plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
}

allprojects {
    group = 'com.wearei'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.annotationProcessorPath = configurations.annotationProcessor
    }

    tasks.withType(Jar) {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    dependencies {
        // Spring Boot JPA
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        // Spring Boot Web
        implementation 'org.springframework.boot:spring-boot-starter-web'

        //Querydsl 추가
        implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
        annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
        annotationProcessor "jakarta.annotation:jakarta.annotation-api"
        annotationProcessor "jakarta.persistence:jakarta.persistence-api"

        // Lombok
        implementation 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // MySQL 커넥터
        runtimeOnly 'com.mysql:mysql-connector-j'

        // Hypersistence Utils
        implementation 'io.hypersistence:hypersistence-utils-hibernate-60:3.3.1'

        // 테스트 라이브러리
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

repositories {
    mavenCentral()
}


project(":api") {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

        compileOnly 'io.jsonwebtoken:jjwt-api:0.11.5'
        runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
        runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

        testImplementation 'org.springframework.security:spring-security-test'

        implementation project(":core")
        implementation project(":common")
        implementation project(":integration")
    }

    tasks.named("bootJar") {
        enabled = true
    }

    jar {
        enabled = true
    }
}

project(":chat") {
    dependencies {
        implementation project(":core")
        implementation project(":common")
        implementation project(":integration")

        // websocket
        implementation 'org.springframework.boot:spring-boot-starter-websocket'
    }

    tasks.named("bootJar") {
        enabled = true
    }

    jar {
        enabled = true
    }
}

project(":core") {
    dependencies {
        implementation project(":common")
        implementation project(":integration")
        // Redisson
        implementation 'org.redisson:redisson-spring-boot-starter:3.37.0'

    }
}

project(":common") {
    tasks.named("bootJar") {
        enabled = false
    }

    jar {
        enabled = true
    }
}

project(":integration") {
    dependencies {
        // AWS S3 연동
        implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

        // Redis
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'
        // Redisson
        implementation 'org.redisson:redisson-spring-boot-starter:3.37.0'
        // websocket
        implementation 'org.springframework.boot:spring-boot-starter-websocket'

        // AWS SQS
        implementation platform("io.awspring.cloud:spring-cloud-aws-dependencies:3.0.1")
        implementation 'io.awspring.cloud:spring-cloud-aws-starter-sqs'
    }

    tasks.named("bootJar") {
        enabled = false
    }

    jar {
        enabled = true
    }
}
